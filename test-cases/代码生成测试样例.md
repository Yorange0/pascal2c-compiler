## 单语法成分测试
### write/writeln语句
#### 关注点
1. 编译成功，运行成功
2. writeln不带参数地调用，转化为printf("\n");
3. 输出bool类型时，会转化为用大括号括起来的if判断语句
4. 不同格式的表达式正确对应到不同的格式控制符

#### PASCAL源程序
<pre>
program test(input,output);
const a=1; //整型常量
      b=3.0; //浮点型常量
      c='c'; //字符型常量
      blank=' '; //空格
var   d:integer; //整型变量
      e:real; //浮点型变量
      f:char; //字符型变量
      g:boolean; //布尔型变量
begin
    d:=6;
    e:=6.0;
    f:='f';
    g:=2>1;//不支持true常量，所以这么写
    writeln(a,blank,b,blank,c,blank,1,blank,1.0,blank,'y');
    write(a,blank,b,blank,c,blank,1,blank,1.0,blank,'y');
    writeln;
    writeln(g);
    writeln(a+b,blank,a*b,blank,g,blank,a>b,blank,c,blank,d,blank,e,blank,d<>e);
end.
</pre>

#### C程序用例
无

### read语句
#### 关注点
1. 编译成功，运行成功
2. 生成的C代码中，不同格式的表达式正确对应到不同的格式控制符
3. 生成的C代码中，读取到普通变量时，正确添加了取地址符
4. 生成的C代码中，读取到引用(指针)参数时，不需要添加取地址符，因为指针本身就已经代表地址了
5. 运行后，正确读取所有值，并原样输出所有值
#### PASCAL源程序
<pre>
program test(input,output);
var   a,i:integer; //整型变量
      b:real; //浮点型变量
      c:char; //字符型变量
      d:array[1..6] of integer; //数组
procedure pro(var a:integer);
begin
    read(a);
end;
begin
    read(c,a); //读取字符和整数
    for i:=1 to 6 do
        pro(d[i]); //循环加调用程序读取6个整数
    read(b); //读取浮点数

    writeln(c,' ',a); //输出字符和整数
    for i:=1 to 6 do //输出6个整数，空格分隔
        write(d[i],' '); 
    writeln; //输出换行
    writeln(b); //输出浮点数
end.
</pre>

#### C程序
<pre>
c 100
1 2 3 4 5 6
2.333
</pre>

### 头文件
#### 关注点
#### PASCAL-S源程序
<pre>
program test(input,output);
var a,b:integer;
begin
    a:=3*b;
end.
</pre>

<pre>
program test(input,output);
var a,b:integer;
begin
    read(b);
    a:=3*b;
    writeln(a);
end.
</pre>

#### C程序用例
第二个程序的测试用例
<pre>
100
</pre>

### 运算符和表达式
#### 关注点
1. 生成的C代码中，各种运算符均转化正确
2. 生成的C代码中，对于a:=--b这种连着的单目运算符，添加了括号，更加易读
3. 生成的C代码中，对于不同类型的表达式，输出格式控制符正确
4. bool类型表达式的输出转化为了if-else语句
5. 输出结果符合预设值

#### PASCAL-S源程序
<pre>
program test(input,output);
const a=3;
      b=1.0;
      c='c';
var d,e,f:integer;
    g,h,i:real;
    j,k,l:char;
    m,n,o:boolean;
function fun(a:integer):integer;
begin
    exit(a+1);
end;
begin
    d:=5; e:=10;
    g:=0.25; h:=0.5;
    j:='a'; k:='b';

    f:= d mod (e div fun(d)); //涉及mod、div和函数调用的复杂表达式
    writeln(f); //类型为integer

    g:= --d + e / d * ( - b); //涉及加减乘除、取相反数、加括号的复杂表达式
    writeln(g); //类型为real

    o:= ( ( f mod e ) <> d ) or ( a > b); //涉及<>、or的复杂表达式
    writeln(o); //类型为boolean

    m:= not not ( ( d + e ) = f ); //涉及取非、=的复杂表达式
    writeln(m); //类型为boolean

    n:= ( g * e < f ) and ( (f mod e) >= d ); //涉及and、<、>=、mode的复杂表达式
    writeln(n); //类型为boolean

    writeln( j <= c); //比较char的大小关系

    writeln( m > n); //比较boolean的大小关系
end.
</pre>

### 引用参数与指针
#### 关注点
1. PASCAL-S源程序中普通变量作为引用形参对应的实参调用时，在生成的C代码中加上&取地址符
2. PASCAL-S源程序中引用参数作为引用形参对应的实参调用时，在生成的C代码中无需前置任何符号，因为引用参数对应到C代码中已经是指针了，所以调用时，传本身就已经是地址了。
3. PASCAL-S源程序中，用到引用参数的地方（除了2中的情况），在生成的C代码中，都需加上前置的"*"，表示解引用
#### PASCAL-S源程序
<pre>
program test(input, output);
var ans:integer;
{
    求斐波那契数列值的过程，利用引用参数保存结果;
    n表示求第n项斐波那契数列值;
    fib(0)=1, fib(1)=1, ……, fib(n)=fib(n-1)+fib(n-2);
}
procedure fib(n:integer;var res:integer);
var tmp1,tmp2:integer;
begin
    if n=0 then
        res:=1
    else
        if n=1 then
            res:=1
        else
        begin
            fib(n-1,tmp1);
            fib(n-2,tmp2);
            res:=tmp1+tmp2;
        end;
end;
{
    自己编写的输出函数，可以实现输出n次out值;
    递归调用自身，n每次减1;
    out值采用的是引用参数;
}
procedure mywrite(n:integer; var out:integer);
begin
    if n=0 then
        exit
    else
    begin
        writeln(out);
        mywrite(n-1, out);
    end;
end;

begin
    fib(10, ans); //ans=fib(10)
    mywrite(3, ans); //输出ans 3次
end.
</pre>

#### C程序用例
无

### 常量和变量定义
#### 关注点
1. 生成的C代码中，各种类型的常量定义均正确的加上了类型关键字，类型关键字的转化正确;
2. 生成的C代码中，各种类型的变量定义正确;
3. 生成的C代码中，各种类型的数组定义正确，各维大小正确;

#### PASCAL-S源程序
<pre>
program test(input, output);
const a=1; //整型常量定义
      b=2.0; //浮点型常量定义
      c='c'; //字符型常量定义
var   d,dd,ddd:integer; //整型变量定义
      e,ee,eee:real; //浮点型变量定义
      f,ff,fff:char; //字符型变量定义
      g,gg,ggg:boolean; //boolean型变量定义
      h,hh,hhh:array[1..3] of integer; //整型数组定义
      i,ii,iii:array[1..3, 4..9] of real; //浮点型数组定义
      j,jj,jjj:array[1..3, 4..9, 10..18] of char; //字符型数组定义
      k,kk,kkk:array[1..3, 4..9, 10..18, 19..30] of boolean; //布尔型数组定义
procedure pro;
const a=1; //整型常量定义
      b=2.0; //浮点型常量定义
      c='c'; //字符型常量定义
var   d,dd,ddd:integer; //整型变量定义
      e,ee,eee:real; //浮点型变量定义
      f,ff,fff:char; //字符型变量定义
      g,gg,ggg:boolean; //boolean型变量定义
      h,hh,hhh:array[1..3] of integer; //整型数组定义
      i,ii,iii:array[1..3, 4..9] of real; //浮点型数组定义
      j,jj,jjj:array[1..3, 4..9, 10..18] of char; //字符型数组定义
      k,kk,kkk:array[1..3, 4..9, 10..18, 19..30] of boolean; //布尔型数组定义
begin
      
end;
begin
    
end.
</pre>
#### C程序用例
无

### 程序声明
#### 关注点
1. 生成的C代码中，子程序声明正确;
2. 生成的C代码中，子程序定义的子程序头正确;
3. 生成的C代码中，子程序声明和子程序头的参数的声明顺序与PASCAL-S源程序一致
4. 生成的C代码中，子程序声明和子程序头的参数中，PASCAL-S源程序的引用参数成功的转化为了指针
#### PASCAL-S源程序
<pre>
program test(input,output);
procedure pro(var a,b,c:integer; d:real; e,f:char; var g,h,i:boolean);
begin

end;
function fun(a:integer; var b,c,d:real; var e,f,g:boolean; h,i:char):real;
begin
    fun:=1;
end;
begin
    
end.
</pre>

#### C程序用例
无

### 数组定义与数组元素引用
#### 关注点
1. 生成的C代码中，数组定义的各维大小正确
2. 生成的C代码中，两个三重循环中，对数组元素的引用，下标表达式添加了正确的偏移量
#### PASCAL-S源程序
<pre>
program test(input,output);
var i,j,k:integer;
    a:array[0..1, 2..3, 4..5] of integer; //数组定义
begin
    for i:=0 to 1 do
        for j:=2 to 3 do
            for k:=4 to 5 do
                read(a[i,j,k]); //三重循环，读入每一个数组元素，共8个
    for i:=0 to 1 do
        for j:=2 to 3 do
            for k:=4 to 5 do
                writeln(a[i,j,k]) //三重循环，输出每一个数组元素，换行符分隔
end.
</pre>
#### C程序用例
<pre>
1 2 3 4 5 6 7 8
</pre>

### 返回语句
#### 关注点
1. 赋值形式的返回值语句正确的转化为了C程序中的return语句;
2. exit带参数的返回值语句正确的转化为了C程序中的return语句;
3. exit不带参数的返回语句正确的转化为了C程序中的return语句;
4. 输出结果符合预设值
#### PASCAL-S源程序
<pre>
program test(input,output);
var b,c,d:integer;
procedure pro;
begin
    exit;
end;
function fun(a:integer):char;
begin
    if a<60 then
        exit('l')
    else
        if a=60 then
            fun:='m'
        else
            exit('h');
end;
function fun2(a:integer; var b,c,d:integer):integer;
begin
    if a=0 then
        fun2:=b+c+d
    else
        if a=1 then
            fun2:=b*c*d
        else
            exit(-b-c-d);
end;
begin
    read(b,c,d);
    writeln(fun(b), ' ', fun(c), ' ', fun(d));
    writeln(fun2(0, b, c, d), ' ', fun2(1, b, c, d), ' ', fun2(2, b, c, d));
end.
</pre>
#### C程序用例
<pre>
59 60 61
</pre>

### 程序的无参调用
#### 关注点
1. 过程的无参调用正确的转化为了C代码，添加了一对括号
2. 函数的无参调用正确的转化为了C代码，添加了一对括号
#### PASCAL-S源程序
<pre>
program test(input,output);
var a:integer;
procedure pro;
begin
    writeln(a);
end;
function fun:integer;
begin
    fun:=100;
end;
begin
    writeln(fun);
    a:=fun mod 3;
    pro;
end.
</pre>

#### C程序用例
无

### 各种语句混合与缩进
#### 关注点
1. if语句转化正确
2. while语句转化正确
3. for语句转化正确
4. repeat语句转化正确，特别是条件表达式，需要取非
5. compound语句转化正确
6. 语句的缩进美观，且符合设计
7. 输入的10个数正确的被排序，按照从小到大的顺序输出
#### PASCAL-S源程序
<pre>
program test(input, output);
var i:integer;
    a:array[1..10] of integer;
procedure swap(var a,b:integer); //交换两个数的值
var tmp:integer;
begin
    tmp:=a;
    a:=b;
    b:=tmp;
end;
procedure bubbleSort; //冒泡排序
var i,j:integer;
begin
    for i:= 1 to 10 do //for语句
    begin //compound语句
        j:=i+1;
        while j <= 10 do //while语句
        begin //compound语句
            if a[i]>a[j] then //if语句
                swap(a[i],a[j]);
            j:=j+1;
        end
    end;
end;
begin
    i:=1;
    repeat //repeat语句
    begin //compound语句
        read(a[i]);
        i:=i+1
    end
    until i=11;
    bubbleSort;
    for i:= 1 to 10 do //for语句
        write(a[i],' ');
    writeln;
end.
</pre>
#### C程序用例
<pre>
8 3 5 4 0 6 9 7 1 2
</pre>

## 综合测试
### 快速排序
#### PASCAL-S源程序
<pre>
program quicksort(input,output);
var
n,i:integer;
a:array[0..100000] of integer;

procedure kp(l,r:integer);
var
i,j,mid:integer;
begin
    if l>=r then exit;
    i:=l;j:=r;mid:=a[(l+r) div 2];
    repeat
	begin
         while a[i]<mid do i:=i+1;
         while a[j]>mid do j:=j-1;
         if i<=j then
         begin
             a[0]:=a[i];a[i]:=a[j];a[j]:=a[0];
             i:=i+1;j:=j-1;
         end
	end
    until i>j;
    kp(l,j);
    kp(i,r)
end;

begin
    read(n);
    for i:=1 to n do
    read(a[i]);
    kp(1,n);
    for i:=1 to n do
    write(a[i],' ');
end.
</pre>

#### C程序用例
<pre>
10
5 9 2 1 0 8 3 4 6 7
</pre>

### 最大公因数
#### PASCAL源程序
<pre>
program example(input,output);
var n,i,x,y:integer;
function gcd(a,b:integer):integer;
begin
	if b=0 then gcd:=a
	else gcd:=gcd(b,a mod b);
end;
begin
    read(n);
    for i:= 1 to n do
    begin
        read(x,y);
        writeln(gcd(x,y))
    end;
end.
</pre>

#### C程序用例
<pre>
5
100 36
3 0
50 30
12 28
327 198321
</pre>

